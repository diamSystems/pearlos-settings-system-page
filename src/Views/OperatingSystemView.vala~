/*
* Copyright 2020–2021 elementary, Inc. (https://elementary.io)
*           2015 Ivo Nunes, Akshay Shekher
*
* This program is free software; you can redistribute it and/or
* modify it under the terms of the GNU General Public
* License as published by the Free Software Foundation; either
* version 3 of the License, or (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
* General Public License for more details.
*
* You should have received a copy of the GNU General Public
* License along with this program; if not, write to the
* Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
* Boston, MA 02110-1301 USA
*/

public class About.OperatingSystemView : Gtk.Box {
    private static string _bug_url;
    private static string bug_url {
        get {
            if (_bug_url == null) {
                _bug_url = Environment.get_os_info (GLib.OsInfoKey.BUG_REPORT_URL);

                if (_bug_url == null) {
                    _bug_url = "https://docs.elementary.io/contributor-guide/feedback/reporting-issues";
                }
            }

            return _bug_url;
        }
    }

    private const uint64 DISK_WARNING_THRESHOLD_BYTES = 5UL * 1024 * 1024 * 1024;

    private string get_updater_script () {
        var override_path = Environment.get_variable ("DIAM_UPDATER_SCRIPT");
        if (override_path != null && override_path.strip ().length > 0) {
            return override_path;
        }

        return DIAM_UPDATER_DEFAULT_SCRIPT;
    }

    private bool ensure_updater_available () {
        var script = get_updater_script ();
        if (!FileUtils.test (script, FileTest.EXISTS)) {
            system_update_update_status (_("Updater unavailable"), _("Could not find updater at %s").printf (script));
            system_update_show_error_dialog (_("diamOS updater not found"), _("Ensure updaterd.py is installed and accessible."));
            return false;
        }

        return true;
    }

    private async Json.Object? run_updater_command (string[] args, bool requires_sudo = false) throws GLib.Error {
        string[] command = {};

        if (requires_sudo) {
            command += "pkexec";
        }

        command += "python3";
        command += get_updater_script ();

        foreach (var arg in args) {
            command += arg;
        }

        var subprocess = new GLib.Subprocess.newv (
            command,
            GLib.SubprocessFlags.STDOUT_PIPE | GLib.SubprocessFlags.STDERR_PIPE
        );

        string stdout_str;
        string stderr_str;
        yield subprocess.communicate_utf8_async (null, null, out stdout_str, out stderr_str);

        if (!subprocess.get_successful ()) {
            var message = stderr_str.strip (); 
            if (message.length == 0) {
                message = _("Updater command failed");
            }
            throw new GLib.IOError.FAILED (message);
        }

        stdout_str = stdout_str.strip ();
        if (stdout_str.length == 0) {
            return null;
        }

        var parser = new Json.Parser ();
        parser.load_from_data (stdout_str);
        var root = parser.get_root ();

        if (root == null || root.get_node_type () != Json.NodeType.OBJECT) {
            return null;
        }

        return root.get_object ();
    }

    private void system_update_refresh_buttons () {
        system_update_check_button.sensitive = !system_update_busy;
        system_update_download_button.sensitive = !system_update_busy && system_update_can_download;
        system_update_install_button.sensitive = !system_update_busy && system_update_can_install;

        if (system_update_busy) {
            system_update_button_stack.visible_child_name = "busy";
        } else if (system_update_can_install) {
            system_update_button_stack.visible_child_name = "install";
        } else if (system_update_can_download) {
            system_update_button_stack.visible_child_name = "download";
        } else {
            system_update_button_stack.visible_child_name = "check";
        }

        system_update_progress_revealer.reveal_child = system_update_show_progress;

        update_disk_space_warning ();
    }

    private bool query_disk_space (out uint64 free_bytes, out uint64 total_bytes) {
        free_bytes = 0;
        total_bytes = 0;

        try {
            var mount = File.new_for_path ("/");
            var info = mount.query_filesystem_info (GLib.FileAttribute.FILESYSTEM_FREE + "," + GLib.FileAttribute.FILESYSTEM_SIZE, null);

            if (info == null) {
                return false;
            }

            free_bytes = info.get_attribute_uint64 (GLib.FileAttribute.FILESYSTEM_FREE);
            total_bytes = info.get_attribute_uint64 (GLib.FileAttribute.FILESYSTEM_SIZE);
            return true;
        } catch (Error e) {
            warning ("Unable to query disk space: %s", e.message);
            return false;
        }
    }

    private void update_disk_space_warning () {
        if (disk_warning_row == null || disk_warning_icon == null || disk_warning_title_label == null || disk_warning_subtitle_label == null) {
            return;
        }

        disk_warning_row.remove_css_class ("low");

        uint64 free_bytes;
        uint64 total_bytes;
        var have_info = query_disk_space (out free_bytes, out total_bytes);

        if (!have_info) {
            disk_warning_icon.icon_name = "dialog-warning-symbolic";
            disk_warning_title_label.label = _("Available disk space warning");
            disk_warning_subtitle_label.label = _("Unable to determine free space.");
            disk_warning_subtitle_label.visible = true;
            disk_warning_row.add_css_class ("low");
            return;
        }

        var free_string = GLib.format_size (free_bytes);
        bool is_low = free_bytes <= DISK_WARNING_THRESHOLD_BYTES;

        if (is_low) {
            disk_warning_icon.icon_name = "dialog-warning-symbolic";
            disk_warning_title_label.label = _("Available disk space warning");
            disk_warning_subtitle_label.label = _("Only %s free. You may need to clear space before downloading updates.").printf (free_string);
            disk_warning_row.add_css_class ("low");
        } else {
            disk_warning_icon.icon_name = "drive-harddisk-symbolic";
            disk_warning_title_label.label = _("Disk space available");
            disk_warning_subtitle_label.label = _("About %s free for updates.").printf (free_string);
            disk_warning_row.remove_css_class ("low");
        }

        disk_warning_subtitle_label.visible = true;
    }

    private void system_update_start_progress_pulse () {
        if (system_update_progress_pulse_id != 0) {
            return;
        }

        system_update_progress_pulse_id = GLib.Timeout.add (100, () => {
            if (!system_update_busy || !system_update_show_progress) {
                system_update_progress_pulse_id = 0;
                return false;
            }

            system_update_progress.pulse ();
            return true;
        });
    }

    private void system_update_stop_progress_pulse () {
        if (system_update_progress_pulse_id != 0) {
            GLib.Source.remove (system_update_progress_pulse_id);
            system_update_progress_pulse_id = 0;
        }
    }

    private void system_update_set_busy (bool busy, string? message = null) {
        system_update_busy = busy;
        system_update_spinner.visible = busy;

        if (busy) {
            system_update_spinner.start ();
            if (system_update_show_progress) {
                system_update_start_progress_pulse ();
            } else {
                system_update_stop_progress_pulse ();
            }
        } else {
            system_update_spinner.stop ();
            system_update_stop_progress_pulse ();
            system_update_show_progress = false;
            system_update_progress.set_fraction (0);
        }

        if (message != null) {
            system_update_update_status (message, null);
        }

        system_update_refresh_buttons ();
    }

    private void system_update_update_status (string headline, string? details = null, string? icon_name = null) {
        system_update_title_label.label = headline;

        if (icon_name != null && icon_name.strip ().length > 0) {
            system_update_image.icon_name = icon_name;
        }

        if (details != null && details.strip ().length > 0) {
            system_update_subtitle_label.label = GLib.Markup.escape_text (details, -1);
            system_update_subtitle_label.visible = true;
        } else {
            system_update_subtitle_label.label = "";
            system_update_subtitle_label.visible = false;
        }
    }

    private void system_update_show_error_dialog (string title, string message) {
        var dialog = new Granite.MessageDialog (
            title,
            message,
            new ThemedIcon ("dialog-error")
        ) {
            transient_for = (Gtk.Window) get_root (),
            modal = true
        };

        dialog.add_button (_("Close"), Gtk.ResponseType.CLOSE);
        dialog.response.connect (dialog.destroy);
        dialog.present ();
    }

    private Json.Node? get_member (Json.Object obj, string key) {
        if (!obj.has_member (key)) {
            return null;
        }

        return obj.get_member (key);
    }

    private string? get_string_member (Json.Object obj, string key) {
        var node = get_member (obj, key);
        if (node == null) {
            return null;
        }

        switch (node.get_node_type ()) {
            case Json.NodeType.NULL:
                return null;
            case Json.NodeType.VALUE:
                if (node.get_value_type () == typeof (string)) {
                    return node.get_string ();
                }
                return null;
            default:
                return null;
        }
    }

    private bool? get_boolean_member (Json.Object obj, string key) {
        var node = get_member (obj, key);
        if (node == null) {
            return null;
        }

        switch (node.get_node_type ()) {
            case Json.NodeType.NULL:
                return null;
            case Json.NodeType.VALUE:
                if (node.get_value_type () == typeof (bool)) {
                    return node.get_boolean ();
                }
                return null;
            default:
                return null;
        }
    }

    private async void system_update_check () {
        if (system_update_busy) {
            return;
        }

        if (!ensure_updater_available ()) {
            return;
        }

        system_update_set_busy (true, _("Checking for updates…"));

        try {
            var result = yield run_updater_command ({ "check" });

            string? current = null;
            string? latest = null;
            bool update_available = false;

            if (result != null) {
                current = get_string_member (result, "current");
                latest = get_string_member (result, "latest");
                var update_node = get_boolean_member (result, "update_available");
                update_available = update_node != null ? update_node : false;
            }

            system_update_current_version = current;
            system_update_latest_version = latest;

            if (update_available) {
                system_update_can_download = true;
                system_update_can_install = false;
                system_update_downloaded_version = null;

                var summary = _("Current: %s\nLatest: %s").printf (
                    current != null ? current : _("Unknown"),
                    latest != null ? latest : _("Unknown")
                );

                system_update_update_status (_("Update available"), summary, "software-update-available");
            } else {
                system_update_can_download = false;
                system_update_can_install = false;
                system_update_downloaded_version = null;

                string details;
                if (current != null && latest != null) {
                    details = _("Current version %s matches latest %s").printf (current, latest);
                } else if (current != null) {
                    details = _("Current version %s").printf (current);
                } else {
                    details = _("Latest version %s").printf (latest != null ? latest : _("Unknown"));
                }

                system_update_update_status (_("System up to date"), details, "process-completed");
            }
        } catch (Error e) {
            warning ("System update check failed: %s", e.message);
            system_update_update_status (_("Failed to check for updates"), e.message);
            system_update_show_error_dialog (_("Unable to check for updates"), e.message);
        }

        system_update_set_busy (false, null);
        system_update_refresh_buttons ();
    }

    private async void system_update_download () {
        if (system_update_busy || !system_update_can_download) {
            return;
        }

        if (!ensure_updater_available ()) {
            return;
        }

        system_update_show_progress = true;
        system_update_set_busy (true, _("Downloading update…"));
        system_update_progress.set_fraction (0);
        system_update_progress.pulse_step = 0.1;
        system_update_start_progress_pulse ();

        try {
            var result = yield run_updater_command ({ "download" });

            if (result == null) {
                system_update_update_status (_("Download completed"), null, "browser-download");
                system_update_can_download = false;
                system_update_can_install = true;
            }

            var message_value = get_string_member (result, "message");
            if (message_value != null) {
                system_update_update_status (message_value, null, "system-software-update");
                system_update_can_download = false;
                system_update_can_install = false;
                system_update_downloaded_version = null;
                return;
            }

            string? downloaded_version = null;
            downloaded_version = get_string_member (result, "downloaded_version");

            system_update_downloaded_version = downloaded_version;
            system_update_can_download = false;
            system_update_can_install = true;

            string details = "";
            if (downloaded_version != null) {
                details = _("Ready to install version %s").printf (downloaded_version);
            }

            var checksum_value = get_boolean_member (result, "checksum_verified");
            if (checksum_value != null && checksum_value) {
                if (details.length > 0) {
                    details += "\n";
                }
                details += _("Checksum verified");
            }

            system_update_update_status (_("Update downloaded"), details, "software-update-available");
        } catch (Error e) {
            warning ("System update download failed: %s", e.message);
            system_update_update_status (_("Failed to download update"), e.message, "dialog-error");
            system_update_show_error_dialog (_("Unable to download update"), e.message);
            return;
        }
        finally {
            system_update_show_progress = false;
            system_update_set_busy (false, null);
            system_update_refresh_buttons ();
        }
    }

    private async void system_update_install () {
        if (system_update_busy) {
            return;
        }

        if (!ensure_updater_available ()) {
            return;
        }

        system_update_show_progress = true;
        system_update_set_busy (true, _("Installing update…"));
        system_update_progress.set_fraction (0);
        system_update_progress.pulse_step = 0.1;
        system_update_start_progress_pulse ();

        try {
            var result = yield run_updater_command ({ "install" }, true);

            string? installed_version = null;
            if (result != null) {
                installed_version = get_string_member (result, "installed_version");
            }

            if (installed_version == null) {
                system_update_update_status (_("Update installed"), null, "applications-system");
            } else {
                system_update_update_status (
                    _("Update installed"),
                    _("System updated to %s").printf (installed_version)
                );
            }

            system_update_can_install = false;
            system_update_can_download = false;
            system_update_downloaded_version = null;

            system_update_set_busy (false, null);
            system_update_refresh_buttons ();

            system_update_check.begin ();
        } catch (Error e) {
            warning ("System update install failed: %s", e.message);
            system_update_update_status (_("Failed to install update"), e.message, "dialog-error");
            system_update_show_error_dialog (_("Installation failed"), e.message);
            return;
        }
        finally {
            system_update_show_progress = false;
            system_update_set_busy (false, null);
            system_update_refresh_buttons ();
        }
    }

    private static string _documentation_url;
    private static string documentation_url {
        get {
            if (_documentation_url == null) {
                _documentation_url = Environment.get_os_info (GLib.OsInfoKey.DOCUMENTATION_URL);

                if (_documentation_url == null) {
                    _documentation_url = "https://elementary.io/docs/learning-the-basics";
                }
            }

            return _documentation_url;
        }
    }

    private static string _website_url;
    private static string website_url {
        get {
            if (_website_url == null) {
                _website_url = Environment.get_os_info (GLib.OsInfoKey.HOME_URL);

                if (_website_url == null) {
                    _website_url = "https://elementary.io";
                }
            }

            return _website_url;
        }
    }

    private static string _support_url;
    private static string support_url {
        get {
            if (_support_url == null) {
                _support_url = Environment.get_os_info (GLib.OsInfoKey.SUPPORT_URL);

                if (_support_url == null) {
                    _support_url = "https://diamsystems.odoo.com";
                }
            }

            return _support_url;
        }
    }

    private uint64 download_size_remaining = 0;
    private uint64 download_size_max = 0;

    private File? logo_file_light;
    private File? logo_file_dark;
    private Gtk.Picture? logo;
    private Granite.Settings granite_settings;
    private Gtk.StringList packages;
    private SystemUpdate? update_proxy = null;
    private SystemUpdate.CurrentState? current_state = null;
    private const string DIAM_UPDATER_DEFAULT_SCRIPT = "/usr/share/bin/";

    private Gtk.Grid software_grid;
    private Gtk.Image updates_image;
    private Gtk.Label updates_title;
    private Gtk.ProgressBar update_progress_bar;
    private Gtk.Revealer update_progress_revealer;
    private Gtk.Label updates_description;
    private Gtk.Revealer details_button_revealer;
    private Gtk.Stack button_stack;
    private Gtk.ListBoxRow system_update_row;
    private Gtk.Image system_update_image;
    private Gtk.Label system_update_title_label;
    private Gtk.Label system_update_subtitle_label;
    private Gtk.Spinner system_update_spinner;
    private Gtk.Revealer system_update_progress_revealer;
    private Gtk.ProgressBar system_update_progress;
    private Gtk.Stack system_update_button_stack;
    private Gtk.Button system_update_check_button;
    private Gtk.Button system_update_download_button;
    private Gtk.Button system_update_install_button;
    private Gtk.ListBoxRow disk_warning_row;
    private Gtk.Image disk_warning_icon;
    private Gtk.Label disk_warning_title_label;
    private Gtk.Label disk_warning_subtitle_label;
    private bool system_update_busy = false;
    private string? system_update_downloaded_version = null;
    private bool system_update_can_download = false;
    private bool system_update_can_install = false;
    private string? system_update_current_version = null;
    private string? system_update_latest_version = null;
    private bool system_update_show_progress = false;
    private uint system_update_progress_pulse_id = 0;

    construct {
        add_css_class ("operating-system-view");

        granite_settings = Granite.Settings.get_default ();

        var style_provider = new Gtk.CssProvider ();
        style_provider.load_from_resource ("io/elementary/settings/system/OperatingSystemView.css");

        Gtk.StyleContext.add_provider_for_display (Gdk.Display.get_default (), style_provider, Gtk.STYLE_PROVIDER_PRIORITY_APPLICATION);

        var uts_name = Posix.utsname ();

        var logo_icon_name = Environment.get_os_info ("LOGO");
        if (logo_icon_name == "" || logo_icon_name == null) {
            logo_icon_name = "distributor-logo";
        }

        var icon = new Gtk.Image () {
            icon_name = logo_icon_name,
        };

        var logo_overlay = new Gtk.Overlay () {
            valign = START
        };

        // Clamp to enforce a maximum width for the logo regardless of parent allocation
        var logo_clamp = new Adw.Clamp () {
            maximum_size = 180,
            tightening_threshold = 180,
            hexpand = false,
            halign = START
        };

        // Use custom logo files from /usr/share/branding/
        var file_dark = File.new_for_path ("/usr/share/branding/caracal.png");
        var file_light = File.new_for_path ("/usr/share/branding/caracal.png");

        if (file_dark.query_exists () || file_light.query_exists ()) {
            logo_file_light = file_light.query_exists () ? file_light : null;
            logo_file_dark = file_dark.query_exists () ? file_dark : null;
            
            logo = new Gtk.Picture () {
                can_shrink = true,
                content_fit = Gtk.ContentFit.CONTAIN,
                valign = Gtk.Align.START,
                halign = Gtk.Align.START,
                hexpand = false
            };
            // Set max width while maintaining aspect ratio (1002x290 = ~3.46:1)
            logo.set_size_request (140, -1);

            logo_overlay.child = logo;
        } else {
            // Fallback to icon if custom logos not found
            icon.pixel_size = 128;
            logo_overlay.child = icon;
        }

        // Hardcoded OS name
        var pretty_name = "PearlOS Caracal, <b>by diam</b>";

        var title = new Gtk.Label (pretty_name) {
            ellipsize = Pango.EllipsizeMode.END,
            selectable = true,
            use_markup = true,
            xalign = 0,
            hexpand = true
        };
        title.add_css_class (Granite.STYLE_CLASS_H2_LABEL);

        var kernel_version_label = new Gtk.Label ("Unix-compatible kernel %s".printf (uts_name.release)) {
            selectable = true,
            xalign = 0,
            hexpand = true
        };
        kernel_version_label.add_css_class (Granite.STYLE_CLASS_SMALL_LABEL);
        kernel_version_label.add_css_class (Granite.STYLE_CLASS_DIM_LABEL);

        packages = new Gtk.StringList (null);

        updates_image = new Gtk.Image () {
            icon_size = LARGE
        };

        updates_title = new Gtk.Label (null) {
            hexpand = true,
            margin_end = 6,
            xalign = 0
        };

        update_progress_bar = new Gtk.ProgressBar () {
            margin_top = 3
        };

        update_progress_revealer = new Gtk.Revealer () {
            child = update_progress_bar
        };

        updates_description = new Gtk.Label (null) {
            xalign = 0,
            use_markup = true,
            wrap = true
        };
        updates_description.add_css_class (Granite.STYLE_CLASS_SMALL_LABEL);
        updates_description.add_css_class (Granite.STYLE_CLASS_DIM_LABEL);

        var progress_description_box = new Gtk.Box (VERTICAL, 3);
        progress_description_box.append (update_progress_revealer);
        progress_description_box.append (updates_description);

        var update_button = new Gtk.Button.with_label (_("Download"));
        update_button.add_css_class (Granite.STYLE_CLASS_SUGGESTED_ACTION);

        var cancel_button = new Gtk.Button.with_label (_("Cancel"));

        var refresh_button = new Gtk.Button.with_label (_("Refresh"));

        button_stack = new Gtk.Stack () {
            hhomogeneous = false,
            transition_type = CROSSFADE,
            valign = CENTER
        };
        button_stack.add_named (new Gtk.Grid (), "blank");
        button_stack.add_named (update_button, "update");
        button_stack.add_named (cancel_button, "cancel");
        button_stack.add_named (refresh_button, "refresh");

        var details_button = new Gtk.Button.with_label (_("Learn More…")) {
            halign = START,
            has_frame = false,
            margin_top = 6
        };
        details_button.add_css_class ("link");
        details_button.add_css_class (Granite.STYLE_CLASS_SMALL_LABEL);

        details_button_revealer = new Gtk.Revealer () {
            child = details_button
        };

        var automatic_updates_button = new Granite.SwitchModelButton (_("Automatic Updates")) {
            description = _("Updates will be automatically downloaded. They will be installed when this device is restarted.")
        };

        var updates_grid = new Gtk.Grid () {
            column_spacing = 6,
            margin_top = 6,
            margin_end = 6,
            margin_bottom = 6,
            margin_start = 6
        };
        updates_grid.attach (updates_image, 0, 0, 1, 2);
        updates_grid.attach (updates_title, 1, 0);
        updates_grid.attach (progress_description_box, 1, 1);
        updates_grid.attach (button_stack, 2, 0, 1, 2);
        updates_grid.attach (details_button_revealer, 1, 2, 2);

        var updates_list = new Gtk.ListBox () {
            margin_bottom = 12,
            margin_top = 12,
            valign = CENTER,
            show_separators = true,
            selection_mode = NONE,
            hexpand = true
        };
        updates_list.add_css_class ("boxed-list");
        updates_list.add_css_class (Granite.STYLE_CLASS_RICH_LIST);
        system_update_image = new Gtk.Image () {
            icon_size = LARGE,
            icon_name = "system-software-update"
        };

        var system_update_header_label = new Gtk.Label (_("System Update")) {
            xalign = 0,
            hexpand = true
        };
        system_update_header_label.add_css_class (Granite.STYLE_CLASS_H3_LABEL);
        system_update_header_label.add_css_class ("system-update-header-label");

        var system_update_header_box = new Gtk.Box (Gtk.Orientation.HORIZONTAL, 0) {
            margin_start = 12,
            margin_end = 12,
            margin_top = 6,
            margin_bottom = 3
        };
        system_update_header_box.append (system_update_header_label);

        var system_update_header_row = new Gtk.ListBoxRow () {
            focusable = false,
            activatable = false
        };
        system_update_header_row.child = system_update_header_box;
        system_update_header_row.add_css_class ("system-update-header-row");

        system_update_title_label = new Gtk.Label (_("System Update")) {
            xalign = 0,
            hexpand = true
        };
        system_update_title_label.add_css_class ("system-update-title-label");

        system_update_subtitle_label = new Gtk.Label ("") {
            xalign = 0,
            wrap = true,
            use_markup = true,
            hexpand = true
        };
        system_update_subtitle_label.add_css_class (Granite.STYLE_CLASS_SMALL_LABEL);
        system_update_subtitle_label.add_css_class (Granite.STYLE_CLASS_DIM_LABEL);
        system_update_subtitle_label.visible = false;

        system_update_spinner = new Gtk.Spinner () {
            halign = Gtk.Align.START,
            valign = Gtk.Align.CENTER,
            visible = false
        };

        system_update_progress = new Gtk.ProgressBar () {
            hexpand = true,
            show_text = false
        };

        system_update_progress_revealer = new Gtk.Revealer () {
            transition_type = Gtk.RevealerTransitionType.CROSSFADE,
            reveal_child = false,
            child = system_update_progress
        };

        var system_update_status_box = new Gtk.Box (Gtk.Orientation.VERTICAL, 3) {
            hexpand = true
        };
        var system_update_subtitle_box = new Gtk.Box (Gtk.Orientation.HORIZONTAL, 6);
        system_update_subtitle_box.append (system_update_spinner);
        system_update_subtitle_box.append (system_update_subtitle_label);
        system_update_status_box.append (system_update_subtitle_box);
        system_update_status_box.append (system_update_progress_revealer);

        system_update_button_stack = new Gtk.Stack () {
            hhomogeneous = false,
            transition_type = CROSSFADE,
            valign = Gtk.Align.CENTER
        };
        system_update_button_stack.add_named (new Gtk.Grid (), "busy");

        system_update_check_button = new Gtk.Button.with_label (_("Check"));
        system_update_button_stack.add_named (system_update_check_button, "check");

        system_update_download_button = new Gtk.Button.with_label (_("Download"));
        system_update_download_button.add_css_class (Granite.STYLE_CLASS_SUGGESTED_ACTION);
        system_update_button_stack.add_named (system_update_download_button, "download");

        system_update_install_button = new Gtk.Button.with_label (_("Install"));
        system_update_install_button.add_css_class (Granite.STYLE_CLASS_SUGGESTED_ACTION);
        system_update_button_stack.add_named (system_update_install_button, "install");

        system_update_button_stack.visible_child_name = "check";

        var system_update_grid = new Gtk.Grid () {
            column_spacing = 6,
            margin_start = 6,
            margin_end = 6,
            margin_top = 6,
            margin_bottom = 6
        };
        system_update_grid.attach (system_update_image, 0, 0, 1, 2);
        system_update_grid.attach (system_update_title_label, 1, 0);
        system_update_grid.attach (system_update_status_box, 1, 1);
        system_update_grid.attach (system_update_button_stack, 2, 0, 1, 2);

        system_update_row = new Gtk.ListBoxRow ();
        system_update_row.child = system_update_grid;
        system_update_row.focusable = false;
        system_update_row.add_css_class ("system-update-row");

        disk_warning_icon = new Gtk.Image () {
            icon_size = LARGE,
            icon_name = "drive-harddisk-symbolic"
        };

        disk_warning_title_label = new Gtk.Label (_("Available disk space warning")) {
            xalign = 0,
            hexpand = true
        };

        disk_warning_subtitle_label = new Gtk.Label ("") {
            xalign = 0,
            wrap = true,
            hexpand = true
        };
        disk_warning_subtitle_label.add_css_class (Granite.STYLE_CLASS_SMALL_LABEL);
        disk_warning_subtitle_label.add_css_class (Granite.STYLE_CLASS_DIM_LABEL);
        disk_warning_subtitle_label.visible = false;

        var disk_warning_grid = new Gtk.Grid () {
            column_spacing = 6,
            margin_start = 6,
            margin_end = 6,
            margin_top = 6,
            margin_bottom = 6
        };
        disk_warning_grid.attach (disk_warning_icon, 0, 0, 1, 2);
        disk_warning_grid.attach (disk_warning_title_label, 1, 0);
        disk_warning_grid.attach (disk_warning_subtitle_label, 1, 1);

        disk_warning_row = new Gtk.ListBoxRow () {
            focusable = false,
            activatable = false
        };
        disk_warning_row.child = disk_warning_grid;
        disk_warning_row.add_css_class ("disk-warning-row");

        updates_list.append (system_update_header_row);
        updates_list.append (system_update_row);
        updates_list.append (disk_warning_row);
        updates_list.append (updates_grid);
        updates_list.append (automatic_updates_button);

        system_update_check_button.clicked.connect (() => {
            system_update_check.begin ();
        });

        system_update_download_button.clicked.connect (() => {
            system_update_download.begin ();
        });

        system_update_install_button.clicked.connect (() => {
            system_update_install.begin ();
        });

        system_update_update_status (_("System Update"), _("Click “Check” to look for updates."), "system-software-update");
        system_update_refresh_buttons ();
        update_disk_space_warning ();

        updates_list.get_first_child ().focusable = false;
        updates_list.get_last_child ().focusable = false;

        var thebasics_link = new LinkRow (
            documentation_url,
            _("Basics Guide"),
            "text-x-generic-symbolic",
            "green"
        );

        var support_link = new LinkRow (
            support_url,
            _("Get Help"),
            "help-contents-symbolic",
            "blue"
        );

        var website_link = new LinkRow (
            website_url,
            _("Our Website"),
            "view-reader-symbolic",
            "slate"
        );

        var links_list = new Gtk.ListBox () {
            margin_bottom = 12,
            margin_top = 12,
            valign = CENTER,
            show_separators = true,
            selection_mode = NONE,
            hexpand = true
        };
        links_list.add_css_class ("boxed-list");
        links_list.add_css_class (Granite.STYLE_CLASS_RICH_LIST);
        links_list.append (website_link);

        var settings_restore_button = new Gtk.Button.with_label (_("Restore Default Settings"));

        var bug_button = new Gtk.Button.with_label (_("Send Feedback")) {
            halign = END,
            hexpand = true
        };

        var button_grid = new Gtk.Box (HORIZONTAL, 6) {
            margin_start = 12,
            margin_end = 12,
            margin_bottom = 12
        };
        button_grid.append (settings_restore_button);
        button_grid.append (bug_button);

        software_grid = new Gtk.Grid () {
            column_spacing = 32,
            margin_top = 12,
            valign = Gtk.Align.START,
            vexpand = true,
            hexpand = true
        };
        // Put the overlay into the clamp so the logo cannot exceed our desired width
        logo_clamp.child = logo_overlay;
        software_grid.attach (logo_clamp, 0, 0, 1, 4);
        software_grid.attach (title, 1, 0);

        software_grid.attach (kernel_version_label, 1, 2);
        software_grid.attach (updates_list, 1, 3);
        software_grid.attach (links_list, 1, 4);

        var clamp = new Adw.Clamp () {
            child = software_grid,
            margin_top = 12,
            margin_end = 12,
            margin_bottom = 12,
            margin_start = 12
        };

        var scrolled_window = new Gtk.ScrolledWindow () {
            child = clamp
        };

        orientation = Gtk.Orientation.VERTICAL;
        spacing = 12;
        append (scrolled_window);
        append (button_grid);

        var system_updates_settings = new Settings ("io.elementary.settings-daemon.system-update");
        system_updates_settings.bind ("automatic-updates", automatic_updates_button, "active", DEFAULT);

        settings_restore_button.clicked.connect (settings_restore_clicked);

        bug_button.clicked.connect (() => {
            var appinfo = new GLib.DesktopAppInfo ("io.elementary.feedback.desktop");
            if (appinfo != null) {
                try {
                    appinfo.launch (null, null);
                } catch (Error e) {
                    critical (e.message);
                    launch_uri (bug_url);
                }
            } else {
                launch_uri (bug_url);
            }
        });

        links_list.row_activated.connect ((row) => {
            launch_uri (((LinkRow) row).uri);
        });

        Bus.get_proxy.begin<SystemUpdate> (SESSION, "io.elementary.settings-daemon", "/io/elementary/settings_daemon", 0, null, (obj, res) => {
            try {
                update_proxy = Bus.get_proxy.end (res);

                update_proxy.state_changed.connect (update_state);
                update_state.begin ();
            } catch (Error e) {
                critical ("Failed to get updates proxy");
            }
        });

        update_button.clicked.connect (() => {
            if (update_proxy != null) {
                update_proxy.update.begin ((obj, res) => {
                    try {
                        update_proxy.update.end (res);
                    } catch (Error e) {
                        critical ("Failed to update: %s", e.message);
                    }
                });
            }
        });

        cancel_button.clicked.connect (() => {
            if (update_proxy != null) {
                update_proxy.cancel.begin ((obj, res) => {
                    try {
                        update_proxy.cancel.end (res);
                    } catch (Error e) {
                        critical ("Failed to cancel update: %s", e.message);
                    }
                });
            }
        });

        refresh_button.clicked.connect (refresh_clicked);

        details_button.clicked.connect (details_clicked);

        // Listen for theme changes and reload logo
        granite_settings.notify["prefers-color-scheme"].connect (() => {
            load_logo.begin ();
        });
    }

    public async void load_logo () {
        if (logo == null) {
            return;
        }

        // Determine which logo to use based on theme
        // In light mode, show dark logo. In dark mode, show light (white) logo.
        File? logo_file = null;
        if (granite_settings.prefers_color_scheme == Granite.Settings.ColorScheme.DARK) {
            logo_file = logo_file_light != null ? logo_file_light : logo_file_dark;
        } else {
            logo_file = logo_file_dark != null ? logo_file_dark : logo_file_light;
        }

        if (logo_file == null) {
            return;
        }

        try {
            var bytes = yield logo_file.load_bytes_async (null, null);
            logo.paintable = Gdk.Texture.from_bytes (bytes);
        } catch (Error e) {
            warning ("Failed to load logo file: %s", e.message);
        }
    }

    private async void update_state () {
        if (update_proxy == null) {
            return;
        }

        try {
            current_state = yield update_proxy.get_current_state ();
        } catch (Error e) {
            updates_image.icon_name = "dialog-error";
            updates_title.label = _("System updates not available");
            updates_description.label = _("Couldn't connect to the backend. Try logging out to resolve the issue.");
            button_stack.visible_child_name = "blank";

            critical ("Failed to get current state from Updates Backend: %s", e.message);
            return;
        }

        update_progress_revealer.reveal_child = false;
        details_button_revealer.reveal_child = current_state.state == AVAILABLE || current_state.state == ERROR;

        switch (current_state.state) {
            case UP_TO_DATE:
                updates_image.icon_name = "process-completed";
                updates_title.label = _("Up To Date");

                try {
                    var last_refresh_time = yield update_proxy.get_last_refresh_time ();
                    updates_description.label = _("Last checked %s").printf (
                        Granite.DateTime.get_relative_datetime (
                            new DateTime.from_unix_utc (last_refresh_time)
                        )
                    );
                } catch (Error e) {
                    critical ("Failed to get last refresh time from Updates Backend: %s", e.message);
                    updates_description.label = _("Last checked unknown");
                }

                button_stack.visible_child_name = "refresh";
                break;
            case CHECKING:
                updates_image.icon_name = "emblem-synchronized";
                updates_title.label = _("Checking for Updates");
                updates_description.label = current_state.message;
                button_stack.visible_child_name = "blank";
                break;
            case AVAILABLE:
                updates_image.icon_name = "software-update-available";
                updates_title.label = _("Updates Available");
                button_stack.visible_child_name = "update";

                try {
                    var details = yield update_proxy.get_update_details ();
                    updates_description.label = dngettext (
                        GETTEXT_PACKAGE,
                        "%i update available (%s)",
                        "%i updates available (%s)",
                        details.packages.length
                    ).printf (details.packages.length, GLib.format_size (details.size));

                    if (Pk.Info.SECURITY in details.info) {
                        updates_image.icon_name = "software-update-urgent";
                    }

                    packages.splice (0, packages.get_n_items (), details.packages);
                } catch (Error e) {
                    updates_description.label = _("Unable to determine number of updates");
                    warning ("Failed to get updates list from backend: %s", e.message);
                }
                break;
            case DOWNLOADING:
                update_progress_revealer.reveal_child = current_state.percentage > 0;
                update_progress_bar.fraction = current_state.percentage / 100.0;

                download_size_remaining = current_state.download_size_remaining;
                if (download_size_remaining > download_size_max) {
                    download_size_max = download_size_remaining;
                }

                updates_image.icon_name = "browser-download";
                updates_title.label = _("Downloading Updates");
                updates_description.label = "%s <span font-features='tnum'>%s</span>".printf (
                    current_state.message,
                    to_progress_text (download_size_remaining, download_size_max)
                );
                button_stack.visible_child_name = "cancel";
                break;
            case RESTART_REQUIRED:
                updates_image.icon_name = "system-reboot";
                updates_title.label = _("Restart to install pending updates");
                updates_description.label = _("Updates have been downloaded. A restart is required to finish installing them.");
                button_stack.visible_child_name = "blank";
                break;
            case ERROR:
                updates_image.icon_name = "dialog-error";
                updates_title.label = _("Failed to download updates");
                updates_description.label = _("Manually refreshing updates may resolve the issue");
                button_stack.visible_child_name = "refresh";
                break;
        }
    }

    private string to_progress_text (uint64 remain_size, uint64 total_size) {
        if (total_size == 0) {
            return "";
        }

        uint64 current_size = total_size - remain_size;
        string current_size_str = GLib.format_size (current_size);
        string total_size_str = GLib.format_size (total_size);

        return _("%s of %s").printf (current_size_str, total_size_str);
    }

    private void details_clicked () {
        if (current_state == null) {
            return;
        }

        if (current_state.state == ERROR) {
            var message_dialog = new Granite.MessageDialog (
                _("Failed to download updates"),
                _("This may have been caused by sideloaded or manually compiled software, a third-party software source, or a package manager error. Manually refreshing updates may resolve the issue."),
                new ThemedIcon ("dialog-error")
            ) {
                transient_for = (Gtk.Window) get_root (),
                modal = true
            };

            message_dialog.show_error_details (current_state.message);

            message_dialog.response.connect (message_dialog.destroy);
            message_dialog.present ();
            return;
        }

        var details_dialog = new UpdateDetailsDialog (packages) {
            transient_for = (Gtk.Window) get_root ()
        };
        details_dialog.present ();
    }

    private async void refresh_clicked () {
        if (update_proxy == null) {
            return;
        }

        try {
            yield update_proxy.check_for_updates (true, false);
        } catch (Error e) {
            critical ("Failed to check for updates: %s", e.message);
        }
    }

    private void launch_uri (string uri) {
        var uri_launcher = new Gtk.UriLauncher (uri);
        uri_launcher.launch.begin (
            ((Gtk.Application) GLib.Application.get_default ()).active_window,
            null
        );
    }

    private void settings_restore_clicked () {
        var dialog = new Granite.MessageDialog (
            _("System Settings Will Be Restored to The Factory Defaults"),
            _("All system settings and data will be reset to the default values. Personal data, such as music and pictures, will be unaffected."),
            new ThemedIcon ("preferences-system"),
            Gtk.ButtonsType.CANCEL
        ) {
            badge_icon = new ThemedIcon ("edit-clear"),
            transient_for = (Gtk.Window) this.get_root ()
        };

        var continue_button = dialog.add_button (_("Restore Settings"), Gtk.ResponseType.ACCEPT);
        continue_button.add_css_class (Granite.STYLE_CLASS_DESTRUCTIVE_ACTION);

        dialog.response.connect ((response) => {
            dialog.destroy ();
            if (response == Gtk.ResponseType.ACCEPT) {
                var all_schemas = get_pantheon_schemas ();

                foreach (var schema in all_schemas) {
                    reset_recursively (schema);
                }
            }
        });
        dialog.present ();
    }

    private static void reset_all_keys (GLib.Settings settings) {
        var schema = SettingsSchemaSource.get_default ().lookup (
            settings.schema_id,
            true
        );

        foreach (var key in schema.list_keys ()) {
            settings.reset (key);
        }
    }

    private static string[] get_pantheon_schemas () {
        string[] schemas = {};
        string[] pantheon_schemas = {};
        string[] prefixes = {
            "io.elementary.desktop",
            "io.elementary.dock",
            "io.elementary.onboarding",
            "io.elementary.settings",
            "io.elementary.settings-daemon",
            "io.elementary.wingpanel",
            "org.gnome.desktop",
            "org.gnome.settings-daemon",
            "org.pantheon.desktop"
        };

        var sss = SettingsSchemaSource.get_default ();

        sss.list_schemas (true, out schemas, null);

        foreach (var schema in schemas) {
            foreach (var prefix in prefixes) {
                if (schema.has_prefix (prefix)) {
                    pantheon_schemas += schema;
                }
            }
        }
        return pantheon_schemas;
    }

    private static void reset_recursively (string schema) {
        var settings = new GLib.Settings (schema);
        // change into delay mode
        // so changes take place when apply () is called
        settings.delay ();

        reset_all_keys (settings);

        foreach (var child in settings.list_children ()) {
            var child_settings = settings.get_child (child);

            reset_all_keys (child_settings);
        }
        settings.apply ();
        GLib.Settings.sync ();
    }

    private class LinkRow : Gtk.ListBoxRow {
        public string uri { get; construct; }
        public string icon_name { get; construct; }
        public string label_string { get; construct; }
        public string color { get; construct; }

        public LinkRow (string uri, string label_string, string icon_name, string color) {
            Object (
                uri: uri,
                label_string: label_string,
                icon_name: icon_name,
                color: color
            );
        }

        class construct {
            set_accessible_role (LINK);
        }

        construct {

            var image = new Gtk.Image.from_icon_name (icon_name) {
                pixel_size = 16
            };
            image.add_css_class (Granite.STYLE_CLASS_ACCENT);
            image.add_css_class (color);

            var left_label = new Gtk.Label (label_string) {
                hexpand = true,
                xalign = 0
            };

            var link_image = new Gtk.Image.from_icon_name ("adw-external-link-symbolic");

            var box = new Gtk.Box (HORIZONTAL, 0);
            box.append (image);
            box.append (left_label);
            box.append (link_image);

            child = box;
            add_css_class ("link");
        }
    }

    private class SponsorUsRow : Gtk.ListBoxRow {
        public string uri { get; construct; }

        private Gtk.Label target_label;
        private Gtk.LevelBar levelbar;
        private Gtk.Revealer details_revealer;

        public SponsorUsRow (string uri) {
            Object (
                uri: uri
            );
        }

        public bool was_loaded {
            get {
                return details_revealer.reveal_child;
            }
        }

        class construct {
            set_accessible_role (LINK);
        }

        construct {
            var image = new Gtk.Image.from_icon_name ("face-heart-symbolic");
            image.add_css_class (Granite.STYLE_CLASS_ACCENT);
            image.add_css_class ("pink");

            var main_label = new Gtk.Label (_("Sponsor Us")) {
                halign = START,
                hexpand = true
            };

            target_label = new Gtk.Label (null) {
                halign = START
            };
            target_label.add_css_class (Granite.STYLE_CLASS_DIM_LABEL);
            target_label.add_css_class (Granite.STYLE_CLASS_SMALL_LABEL);

            levelbar = new Gtk.LevelBar ();
            levelbar.add_css_class (Granite.STYLE_CLASS_FLAT);
            levelbar.add_css_class ("pink");

            var details_box = new Gtk.Box (VERTICAL, 0);
            details_box.append (target_label);
            details_box.append (levelbar);

            details_revealer = new Gtk.Revealer () {
                child = details_box,
                reveal_child = false
            };

            var link_image = new Gtk.Image.from_icon_name ("adw-external-link-symbolic");

            var grid = new Gtk.Grid () {
                valign = CENTER
            };
            grid.attach (image, 0, 0, 1, 2);
            grid.attach (main_label, 1, 0);
            grid.attach (details_revealer, 1, 1);
            grid.attach (link_image, 2, 0, 2, 2);

            child = grid;
            add_css_class ("link");
        }

        public void get_goal_progress (GLib.Cancellable cancellable) {
            var message = new Soup.Message ("GET", "https://elementary.io/api/sponsors_goal");
            var session = new Soup.Session ();
            session.send_and_read_async.begin (message, GLib.Priority.DEFAULT, cancellable , (obj, res) => {
                try {
                    var bytes = session.send_and_read_async.end (res);

                    var output = (string) bytes.get_data ();
                    if (output == null) {
                        return;
                    }

                    var parser = new Json.Parser ();
                    parser.load_from_data (output);

                    var root = parser.get_root ();
                    if (root.get_node_type () != OBJECT) {
                        return;
                    }

                    int64 percent_complete = root.get_object ().get_int_member ("percent");
                    double target_value = root.get_object ().get_double_member ("target");

                    var animation_target = new Adw.CallbackAnimationTarget ((val) => {
                        ///TRANSLATORS: first value is a percentage, second value is an amount in USD
                        target_label.label = _("%.0f%% towards $%'5.0f per month goal").printf (
                            Math.round (val),
                            target_value
                        );

                        levelbar.value = val / 100.0;
                    });

                    var animation = new Adw.TimedAnimation (
                        this, 0, percent_complete, 1000,
                        animation_target
                    ) {
                        easing = EASE_IN_OUT_QUAD
                    };

                    details_revealer.reveal_child = true;
                    animation.play ();
                } catch (Error e) {
                    critical (e.message);
                }
            });
        }
    }
}
